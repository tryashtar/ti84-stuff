ClrHome
{5000,5000,0,100,100,0,1,1,1,0,0,⁻1,1,0,5000,1,0,0,0,0,0,0,0,0,0,0,0,1,0,0,0,1,0,0,0,0,0,0,0,0,0,0}→ʟCANDY
{5,3,4,4,2,6,4,6,6,4,5,6,6}→L₁
{0,3,1,3,2,4,4,1,1,1,1,1,1}→L₂
{0,1,2,3,4,5,6,7,8,9,10,11}→L₄
{20.0110,200.5020,15.3103,50.1014,100.0504,256.8040}→L₆
1→A
1→B
0→C
0→D
startTmr→X
1→θ
0→K
While 1
	0→K
	While K≠105 and K≠45 and θ=0
		0→K
		While K=0 and θ=0
			getKey→K
			startTmr-X→Y
			If Y≥1
			Then
				ʟCANDY(1)+YʟCANDY(13)→ʟCANDY(1
				ʟCANDY(2)+YʟCANDY(14)→ʟCANDY(2
				min(ʟCANDY(4)+Y,ʟCANDY(5))→ʟCANDY(4)
				If ʟCANDY(12)≥0
				Then
					ʟCANDY(12)+Y→ʟCANDY(12
					If A=1
						Output(4,14,ʟCANDY(12
					If A=4
						Output(6,13,ʟCANDY(12
				End
				If L₂(A)=1
					Output(8,5,ʟCANDY(1
				If L₂(A)=2 or L₂(A)=3
					Output(8,5,iPart(ʟCANDY(2
				If L₂(A)=3
					Output(7,5,ʟCANDY(1
				If L₂(A)=4
					Output(8,1,ʟCANDY(4
				startTmr→X
			End
		End
		If C=0
		Then
			If K=26 and (A=6 or A=8)
			Then
				A+1→θ
				1→B
			End
			If K=24 and (A=7 or A=9)
			Then
				A-1→θ
				1→B
			End
			If K=25 or K=34
			Then
				Output(B+1,1," 
				remainder(B+(K=34)-(K=25)+L₁(A),L₁(A))
				Output(B+1,1,"►
			End
		Else
			If K=23
			Then
				iPart(D/10)→D
				Output(B+1,7,"          
				If D>0
					Output(B+1,7,D
			End
			If ((K≥72 and K≤94 and fPart(K/10)≥.2 and fPart(K/10)≤.4) or K=102)
			Then
				If K=102
					0→K
				If K≠0
					26-3iPart(K/10)+10fPart(K/10)→K
				If D<999999
					10D+K→D
				If D>0
					Output(B+1,7,D
			End
		End
	End
	If K=45
	Then
		If C≥1
		Then
			If C=1
				A→θ
			If C=2
			Then
				0→C
				If A=2 and B=1
					Output(2,2,"Eat candies     
				If A=4 and B=1
					Output(2,2,"Add candies     
				If A=4 and B=2
					Output(3,2,"Add lollipops   
				If A=5 and B=1
					Output(2,2,"Plant           
			End
		Else
			If A=1
				Return
			If A≥2 and A≤7
			Then
				1→θ
				A-1→B
				If A=7
					5→B
			End
			If A≥8 and A≤12
			Then
				3→θ
				A-8→B
				If A=8
					1→B
			End
			If A=13
			Then
				4→θ
				4→B
			End
		End
	End
	If K=105
	Then
		If C≥1
		Then
			If C=1
				A→θ
			If C=2 and D>0
			Then
				If A=2 and B=1 and ʟCANDY(1)≥D
				Then
					0→C
					Output(2,2,"Eat more        
					ʟCANDY(1)-D→ʟCANDY(1
					ʟCANDY(3)+D→ʟCANDY(3
					Output(7,5,"            
					Output(7,5,ʟCANDY(1
				End
				If A=4 and B=1 and ʟCANDY(1)≥D
				Then
					0→C
					Output(2,2,"Add candies     
					ʟCANDY(1)-D→ʟCANDY(1
					ʟCANDY(10)+D→ʟCANDY(10
					Output(7,5,"            
					Output(7,5,ʟCANDY(1
				End
				If A=4 and B=2 and ʟCANDY(2)≥D
				Then
					0→C
					Output(3,2,"Add lollipops   
					ʟCANDY(2)-D→ʟCANDY(2
					ʟCANDY(11)+D→ʟCANDY(11
					Output(8,5,"            
					Output(8,5,iPart(ʟCANDY(2
				End
				If A=5 and B=1 and ʟCANDY(2)≥D
				Then
					0→C
					5→θ
					ʟCANDY(2)-D→ʟCANDY(2
					ʟCANDY(6)+D→ʟCANDY(6
					If ʟCANDY(6)<293
						ʟCANDY(6)²/8640→ʟCANDY(14
					If ʟCANDY(6)≥293
						min((ʟCANDY(6)-122)*5/864,100)→ʟCANDY(14
				End
			End
		Else
			If A=12
			Then
				1→C
				For(I,1,7
					Output(I,1,"                
				End
				If B=1
				Then
					Output(2,1,"Restores 20% of
					Output(3,1,"your max health.
				End
				If B=2
				Then
					Output(2,1,"Restores 50% of
					Output(3,1,"your max health.
				End
				If B=3
				Then
					Output(2,1,"Teleports you to
					Output(3,1,"the beginning
					Output(4,1,"of a quest.
				End
				If B=4
				Then
					Output(2,1,"Deals 5 damage
					Output(3,1,"to everything.
				End
				If B=5
				Then
					Output(2,1,"Shoots a ball of
					Output(3,1,"fire to hurt
					Output(4,1,"enemies.
				End
				If B=6
				Then
					Output(2,1,"Doubles your
					Output(3,1,"strength for a
					Output(4,1,"short time.
				End
				Output(6,1,"Press ? to use!
				Output(6,7,B
			End
			If A=13
			Then
				1→C
				For(I,1,7
					Output(I,1,"                
				End
				Output(3,1,"Candies:
				Output(4,1,"Lollipops:
				Output(5,1,"Stir time:
				Output(3,12,iPart(L₆(B
				Output(4,12,iPart(100fPart(L₆(B
				Output(5,12,iPart(100fPart(100fPart(L₆(B
			End
			If (A=2 and B=1) or (A=4 and B≤2) or (A=5 and B=1)
			Then
				Output(B+1,2,"Num:            
				2→C
				0→D
			End
			If A=4 and B=4
			Then
				13→θ
				1→B
			End
			If A=3
			Then
				B+8→θ
				If ʟCANDY(7)<6 and B=1
					8→θ
				1→B
			End
			If A=1
			Then
				B+1→θ
				1→B
			End
		End
	End
	If θ≥1
	Then
		θ→A
		0→θ
		0→C
		ClrHome
		Output(B+1,1,"►
		If L₂(A)=1
		Then
			Output(8,1,">O<
			Output(8,5,ʟCANDY(1
		End
		If L₂(A)=2 or L₂(A)=3
		Then
			Output(8,1,"--O
			Output(8,5,iPart(ʟCANDY(2
		End
		If L₂(A)=3
		Then
			Output(7,1,">O<
			Output(7,5,ʟCANDY(1
		End
		If L₂(A)=4
		Then
			Output(8,1,ʟCANDY(4
			Output(8,8,"⁄
			Output(8,13,ʟCANDY(5
		End
		If A=1
		Then
			Output(1,7,"Menu
			Output(2,2,"invertedequal Candy Box
			Output(3,2,"$ Equipment
			Output(4,2,"Ú Cauldron
			If ʟCANDY(12)≥0
			Then
				Output(4,2,"δ
				Output(4,14,ʟCANDY(12
			End
			Output(5,2,"Ä Lollipop Farm
			Output(6,2,"! Quests
		End
		If A=2
		Then
			Output(1,4,"Candy Box
			Output(2,2,"Eat candies
			Output(3,2,"Buy lollipop>60
			Output(4,2,"Ful‗l heal   >30
		End
		If A=3
		Then
			Output(1,4,"Equipment
			Output(2,2,"∠ Weapon
			Output(3,2,"Ω Armor
			Output(4,2,"|~ Hat
			Output(5,2,"û Potions
		End
		If A=4
		Then
			Output(1,5,"Cauldron
			Output(2,2,"Add candies
			Output(3,2,"Add lollipops
			Output(4,2,"Stir
			Output(5,2,"Recipes
			If ʟCANDY(12)≥0
				Output(6,13,ʟCANDY(12
			If ʟCANDY(10)>0
				Output(7,13,ʟCANDY(10
			If ʟCANDY(11)>0
				Output(8,13,ʟCANDY(11
		End
		If A=5
		Then
			Output(1,2,"Lollipop Farm
			Output(2,2,"Plant
			Output(3,2,"Feed mill
			Output(6,1,"L⁄?
			If ʟCANDY(14)≥0
			Then
				Output(6,3,"D
				86400→Y
			End
			If ʟCANDY(14)≥1/3600
			Then
				Output(6,3,"H
				3600→Y
			End
			If ʟCANDY(14)≥1/60
			Then
				Output(6,3,"M
				60→Y
			End
			If ʟCANDY(14)≥1
			Then
				Output(6,3,"S
				1→Y
			End
			Output(6,5,round(YʟCANDY(14),2
			Output(7,1,"Planted:
			Output(7,10,ʟCANDY(6
		End
		If A=6
		Then
			Output(1,6,"Quests -‗>
			Output(2,2,"Î Forest
			Output(3,2,"Δ Mt. Goblin
			Output(4,2,"|~ Ocean
			Output(5,2,"Ω Castle
			Output(6,2,"∫ Dark Woods
			Output(7,2,"σ Cave
		End
		If A=7
		Then
			Output(1,3,"<- Quests
			Output(2,2,"bolduparrow Volcano
			Output(3,2,"ᵤ Sunken Abyss
			Output(4,2,"α Dragon Lair
			Output(5,2,"# Tundra
		End
		If A=8
		Then
			Output(1,5,"Weapons -‗>
			Output(2,3,"None
			If ʟCANDY(7)≤6
				Output(ʟCANDY(7)+1,2,":
		End
		If A=9
		Then
			Output(1,2,"<- Weapons
			If ʟCANDY(7)≥7
				Output(ʟCANDY(7)-5,2,":
		End
		If A=10
		Then
			Output(1,6,"Armor
			Output(2,3,"None
			Output(ʟCANDY(8)+1,2,":
		End
		If A=11
		Then
			Output(1,7,"Hats
			Output(2,3,"None
			Output(ʟCANDY(9)+1,2,":
		End
		If A=12 or A=13
		Then
			If A=12
				Output(1,5,"Potions
			If A=13
				Output(1,5,"Recipes
			Output(2,2,"Health I
			Output(3,2,"Health II
			Output(4,2,"Teleport
			Output(5,2,"Acid burst
			Output(6,2,"Fire breath
			Output(7,2,"Berserk
		End
	End
End