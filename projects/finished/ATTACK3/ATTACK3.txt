Lbl 8
ClrHome
If θ≠123698745
Then
	Output(3,5,"ATTACK 3
	Output(4,5,"|~|~|~|~|~|~|~|~
End
Output(6-(3*(θ=123698745)),3,"Yet another
Output(7-(3*(θ=123698745)),1,"monster appears!
0→θ
Pause 
ClrHome
{100,100,400,100,100,400}→L₁
{1,1,1,1,15,20,30,20}→L₂
{92,93,94,82,83,84,72,73,74}→L₃
0→W
0→L
0→F
0→P
0→N
0→G
0→X
0→Y
0→Z
0→θ
0→A
0→S
0→T
0→K
0→I
Output(1,1,"HP   MP   BOSS
Output(2,1,L₁(1
Output(2,6,L₁(2
Output(2,11,L₁(3
1→S
While 1
	For(I,4,8
		Output(I,1,"                
	End
	If L₁(1)*L₁(3)=0
		Goto 5
	If N=1
	Then
		0→N
		If G>0
			max({G-2,0})→G
		If S≠2
			min({L₁(2)+5,L₁(5)})→L₁(2
		Output(2,6,"   
		Output(2,6,L₁(2
		randInt(1,25)→Y
		If Y≤4
		Then
			L₂(Y)+1→L₂(Y
			Output(4,1,"You pick up a
			If Y=1
				Output(5,1,"laser!
			If Y=2
				Output(5,1,"potion!
			If Y=3
				Output(5,1,"shield!
			If Y=4
			Then
				Output(4,14,"n
				Output(5,1,"ether!
			End
			Pause 
			For(I,4,8
				Output(I,1,"                
			End
		End
	End
	If W=0
	Then
		Output(4,2,"Attack
		Output(5,2,"Magic
		Output(6,2,"Items
		0→K
		While K≠105
			Output(S+3,1,"►
			0→K
			While K=0
				getKey→K
			End
			If K=45
			Then
				Output(8,1,"Rea‗lly quit?
				0→K
				While K≠45 and K≠105
					getKey→K
				End
				If K=105
				Then
					ClrHome
					Return
				End
				If K=45
					Output(8,1,"            
			End
			Output(S+3,1," 
			If K=25
				S-1→S
			If K=34
				S+1→S
			If S>3
				1→S
			If S<1
				3→S
		End
		For(I,4,8
			Output(I,1,"                
		End
		If S=1
		Then
			Output(4,2,"Norma‗l hit
			Output(5,2,"Strong blow
			Output(6,2,"Multi-strike
			Output(7,2,"Skill swing
		End
		If S=2
		Then
			Output(4,2,"Fireball
			Output(5,2,"Heal
			Output(6,2,"Conjure
			Output(7,2,"Soul sear
			For(I,1,4
				Output(I+3,15,L₂(I+4
			End
		End
		If S=3
		Then
			Output(4,2,"Laser
			Output(5,2,"Potion
			Output(6,2,"Shield
			Output(7,2,"Ether
			For(I,1,4
				Output(I+3,15,L₂(I
			End
		End
		1→T
		0→K
		While not((K=105 and (S=1 or (S=2 and L₁(2)≥L₂(T+4)) or (S=3 and L₂(T)≥1))) or K=45)
			Output(T+3,1,"►
			0→K
			While K=0
				getKey→K
			End
			Output(T+3,1," 
			If K=25
				T-1→T
			If K=34
				T+1→T
			If T>4
				1→T
			If T<1
				4→T
		End
		For(I,4,8
			Output(I,1,"                
		End
		If K≠45
		Then
			1→N
			If S=1
			Then
				If T=1
				Then
					If randInt(1,10)=1
					Then
						Output(4,1,"You critically
						Output(5,1,"strike!
						randInt(17,26)→Y
					Else
						Output(4,1,"You swing your
						Output(5,1,"weapon and‗ hit!
						randInt(7,15)→Y
					End
					Output(7,1,"Damage:
					Output(7,9,Y
					max({L₁(3)-Y,0})→L₁(3)
				End
				If T=2
				Then
					Output(4,1,"You attack with
					Output(5,1,"great strength!
					randInt(16,29)→Y
					Output(7,1,"Damage:
					Output(7,9,Y
					max({L₁(3)-Y,0})→L₁(3
					randInt(0,1)→W
					If W=1
					Then
						Output(2,11,"    
						Output(2,11,L₁(3
						Pause 
						Output(8,1,"You get tired…
					End
				End
				If T=3
				Then
					randInt(2,3)→X
					For(I,1,4
						If randInt(1,I+2+iPart(X/4))=1
							X+1→X
					End
					0→Z
					For(I,1,X
						Output(4,1,"                
						Output(7,1,"                
						randInt(1,5)→θ
						If θ=1
							Output(4,1,"You hit!
						If θ=2
							Output(4,1,"You attack!
						If θ=3
							Output(4,1,"You swing!
						If θ=4
							Output(4,1,"You strike!
						If θ=5
							Output(4,1,"You fight!
						If randInt(1,5)=1
						Then
							0→Y
							Output(7,1,"Miss!
						Else
							randInt(3,6)→Y
							Output(7,1,"Damage:
							Output(7,9,Y
							max({L₁(3)-Y,0})→L₁(3
						End
						Output(2,11,"   
						Output(2,11,L₁(3
						Z+Y→Z
						Pause 
					End
					For(I,4,8
						Output(I,1,"                
					End
					Output(4,1,"Total hits:
					Output(4,13,X
					Output(7,1,"Total damage:
					Output(7,15,Z
					If randInt(1,15)=1
					Then
						1→P
						Pause 
						Output(8,1,"Paralyzing!
					End
				End
				If T=4
				Then
					{1,2,4,7,11,16,22,29,37,46,56,67,79,92}→ʟSS
					" "→Str0
					randInt(4,9)→θ
					For(I,1,θ
						randInt(3,12→Z
						Str0+sub("OO O  O   O    O     O      O       O        O         O          O           O            O             O",ʟSS(Z),Z)→Str0
					End
					Output(4,1,Str0
					0→Z
					0→K
					For(I,1,30
						getKey→K
						K(102≠K)-13int(K/13(2>abs(5-abs(5-abs(K-83→K
						If K≥1 and K≤9
						Then
							I→J
							25→I
							If K=θ
								1→Z
							If K≠θ
								2→Z
						End
					End
					For(I,4,8
						Output(I,1,"                
					End
					If Z=1
					Then
						Output(4,1,"You skillfully
						Output(5,1,"hit the monster!
						randInt(15,45)+randInt(1,iPart((20-J)/2))→Y
						max({L₁(3)-Y,0})→L₁(3
						Output(7,1,"Damage:
						Output(7,9,Y
					End
					If Z=2
					Then
						Output(4,1,"You fail and
						Output(5,1,"hurt yourself!
						randInt(6,11)→Y
						max({L₁(1)-Y,0})→L₁(1
						Output(7,1,"Health lost:
						Output(7,14,Y
					End
					If Z=0
					Then
						Output(4,1,"You were too
						Output(5,1,"slow to attack…
					End
				End
			End
			If S=2
			Then
				L₁(2)-L₂(T+4)→L₁(2)
				Output(2,6,"    
				Output(2,6,L₁(2
				If T=1
				Then
					Output(4,1,"You cast a ball
					Output(5,1,"of fire!
					max({randInt(11,17)-randInt(iPart(G/2),G),1})→Y
					G+3→G
					Output(7,1,"Damage:
					Output(7,9,Y
					max({L₁(3)-Y,0})→L₁(3
					If randInt(1,5)=1 and F=0
					Then
						Output(2,11,"   
						Output(2,11,L₁(3
						Pause 
						randInt(2,4)→F
						Output(8,1,"It burns!
						Output(8,11,F
					End
				End
				If T=2
				Then
					If randInt(1,20)=1
					Then
						Output(4,1,"Healing light
						Output(5,1,"surrounds you!
						Output(8,1,"No mana used!
						L₁(2)+L₂(6)→L₁(2
						Output(2,6,L₁(2
					Else
						Output(4,1,"You heal your
						Output(5,1,"wounds!
					End
					randInt(13,20)→Y
					Output(7,1,"Healing:
					Output(7,10,Y
					min({L₁(1)+Y,L₁(4)})→L₁(1
				End
				If T=3
				Then
					If randInt(1,100)=1
					Then
						Output(4,1,"You conjure one
						Output(5,1,"of every item!
						L₂(1)+1→L₂(1
						L₂(2)+1→L₂(2
						L₂(3)+1→L₂(3
						L₂(4)+1→L₂(4
					Else
						Output(4,1,"You conjure a
						randInt(1,4)→Y
						L₂(Y)+1→L₂(Y
						If Y=1
							Output(5,1,"laser!
						If Y=2
							Output(5,1,"potion!
						If Y=3
							Output(5,1,"shield!
						If Y=4
						Then
							Output(4,14,"n
							Output(5,1,"ether!
						End
					End
				End
				If T=4
				Then
					Output(4,1,"You sear your
					Output(5,1,"soul to attack!
					randInt(25,40)→Y
					randInt(8,14)→Z
					Output(7,1,"Damage:
					Output(7,9,Y
					Output(8,1,"Health lost:
					Output(8,14,Z
					max({L₁(3)-Y,0})→L₁(3
					max({L₁(1)-Z,0})→L₁(1
				End
			End
			If S=3
			Then
				L₂(T)-1→L₂(T)
				If T=1
				Then
					If randInt(1,20)=1
					Then
						Output(4,1,"You use a deadly
						Output(5,1,"omicron beam!
						randInt(29,41)→Y
					Else
						Output(4,1,"You fire a
						Output(5,1,"powerful laser!
						randInt(22,32)→Y
					End
					Output(7,1,"Damage:
					Output(7,9,Y
					max({L₁(3)-Y,0})→L₁(3
				End
				If T=2
				Then
					randInt(13,23)→Y
					If randInt(1,20)=1
					Then
						Output(4,1,"Healing power
						Output(5,1,"fills your body!
						Output(8,1,"+10 max HP!
						L₁(4)+10→L₁(4
						L₁(1)+10→L₁(1
					Else
						Output(4,1,"You drink a
						Output(5,1,"healing potion!
					End
					Output(7,1,"Health:
					Output(7,9,Y
					min({L₁(1)+Y,L₁(4)})→L₁(1
				End
				If T=3
				Then
					randInt(2,4)→Y
					If randInt(1,10)=1
						Y+1→Y
					L+Y→L
					Output(4,1,"You raise an
					Output(5,1,"iron shield!
					Output(7,1,"Invincible: +
					Output(7,14,Y
				End
				If T=4
				Then
					Output(4,1,"You use an ether
					Output(5,1,"to restore mana!
					randInt(4,7)*5→Y
					Output(7,1,"Mana:
					Output(7,7,Y
					min({L₁(2)+Y,L₁(5)})→L₁(2
				End
			End
			Output(2,1,"                
			Output(2,1,L₁(1
			Output(2,6,L₁(2
			Output(2,11,L₁(3
			Pause 
		End
	Else
		W-1→W
		1→N
		Output(4,1,"You are too weak
		Output(5,1,"to move…
		Pause 
	End
	If K≠45
	Then
		For(I,4,8
			Output(I,1,"                
		End
		If L₁(1)*L₁(3)=0
			Goto 5
		If P>0
		Then
			P-1→P
			Output(4,1,"The monster is
			Output(5,1,"stunned and
			Output(6,1,"cannot move!
		Else
			0→A
			If L₁(3)<L₁(6)/2 and randInt(1,10)=1
				1→A
			If A=0 and L₁(3)<L₁(6)/3 and randInt(1,20)<L and L≥2
				2→A
			If A=0 and L₁(3)<L₁(6)/1.7 and randInt(1,10)=1
				3→A
			If A=0
			Then
				randInt(6,14)→Y
				0→Z
				If randInt(1,100)<100-(50log(max({.3*L₁(3),1})))
				Then
					1→Z
					Output(4,1,"The monster uses
					Output(5,1,"a super attack!
					randInt(13,22)→Y
				Else
					Output(4,1,"The monster
					randInt(1,5)→X
					If X=1
						Output(5,1,"attacks!
					If X=2
						Output(5,1,"strikes!
					If X=3
						Output(5,1,"lunges!
					If X=4
						Output(5,1,"claws!
					If X=5
						Output(5,1,"slashes!
					If randInt(1,200)=1
						Output(5,1,"slaps your face!
				End
				If randInt(1,7)=1
				Then
					Output(7,1,"It missed!
				Else
					If L>0
					Then
						L-1→L
						Output(7,1,"Damage absorbed!
						Output(8,1,"Turns left:
						Output(8,13,L
					Else
						randInt(1,9)→Z
						Output(7,1,"Press  !
						Output(7,7,Z
						0→K
						0→X
						For(I,1,17
							If K=0
								getKey→K
							If K=105
								1000→I
							If K=L₃(Z)
								1→X
						End
						Output(7,1,"                
						If X=0
						Then
							Output(7,1,"Damage:
							Output(7,9,Y
						Else
							iPart(Y/2)→Y
							Output(7,1,"Blocked!
							Output(8,1,"Damage:
							Output(8,9,Y
						End
						max({L₁(1)-Y,0})→L₁(1
						Output(2,1,"   
						Output(2,1,L₁(1
					End
				End
				If A=1
				Then
					Output(4,1,"The monster
					Output(5,1,"clamps its jaws!
					If L>0
					Then
						L-1→L
						Output(7,1,"Damage absorbed!
						Output(8,1,"Turns left:
						Output(8,13,L
					Else
						randInt(6,13)→Y
						randInt(1,9)→Z
						Output(7,1,"Press  !
						Output(7,7,Z
						0→K
						0→X
						For(I,1,17
							If K=0
								getKey→K
							If K=105
								1000→K
							If K=L₃(Z
								1→X
						End
						Output(7,1,"                
						If X=1
						Then
							iPart(Y/2)→Y
							Output(7,1,"Blocked!
							Output(8,1,"Damage:
							Output(8,9,Y
						Else
							randInt(12,22)→Z
							Output(7,1,"Damage:
							Output(8,1,"Healing:
							Output(7,9,Y
							Output(8,10,Z
							min({L₁(3)+Z,L₁(6)})→L₁(3
						End
						max({L₁(1)-Y,0})→L₁(1
						Output(2,1,"    
						Output(2,11,"    
						Output(2,1,L₁(1
						Output(2,11,L₁(3
					End
				End
			End
			If A=2
			Then
				Output(4,1,"The monster
				Output(5,1,"crushes your
				Output(6,1,"shield!
				0→L
			End
			If A=3
			Then
				Output(4,1,"The monster
				Output(5,1,"wildly attacks!
				If L>0
				Then
					L-1→L
					Output(7,1,"Attack absorbed!
					Output(8,1,"Turns left:
					Output(8,13,L
				Else
					Output(7,1,"Press ENTER to
					Output(8,1,"fill the bar!
					0→X
					0→Y
					0→K
					While X<16
						Y+1→Y
						If K=⁻1
							0→K
						If K=105
							⁻1→K
						If K≠⁻1
							getKey→K
						If K=105
						Then
							X+1→X
							If X=1
							Then
								Output(7,1,"                
								Output(8,1,"                
							End
							Output(7,X,"=
						End
						If fPart(Y/20)=0
						Then
							randInt(4,7)→Z
							Output(8,1,"You are hit!
							Output(8,14,Z
							max({L₁(1)-Z,0})→L₁(1)
							Output(2,1,"    
							Output(2,1,L₁(1
							If L₁(1)=0
							Then
								Pause 
								Goto 5
							End
						End
					End
				End
			End
		End
		Pause 
		If L₁(1)*L₁(3)=0
			Goto 5
		If F>0
		Then
			For(I,4,8
				Output(I,1,"                
			End
			F-1→F
			randInt(4,7)→Y
			Output(4,1,"Burns hurt the
			Output(5,1,"monster!
			Output(7,1,"Damage:
			Output(7,9,Y
			Output(8,1,"Turns left:
			Output(8,13,F
			max({L₁(3)-Y,0})→L₁(3
			Output(2,11,"   
			Output(2,11,L₁(3
			Pause 
			If L₁(1)*L₁(3)=0
				Goto 5
		End
	End
End
Lbl 5
For(I,4,8
	Output(I,1,"                
End
If L₁(3)=0
	Output(4,1,"You win!
If L₁(1)=0
	Output(4,1,"You lose…
If L₁(1)+L₁(3)=0
Then
	Output(4,1,"In a final
	Output(5,1,"valiant effort,
	Output(6,1,"you sacrificed
	Output(7,1,"yourself to slay
	Output(8,1,"the foe.
	Pause 
	For(I,4,8
		Output(I,1,"                
	End
	Output(4,1,"Truly you have
	Output(5,1,"learned the
	Output(6,1,"ultimate meaning
	Output(7,1,"of sacrifice.
	Pause 
	For(I,4,8
		Output(I,1,"                
	End
	Output(4,1,"But you still
	Output(5,1,"died, scrub!
End
Pause 
For(I,4,8
	Output(I,1,"                
End
Output(4,1,"Play again?
Output(5,2,"Yes
Output(6,2,"No
1→S
0→K
While K≠105
	Output(S+4,1,"►
	0→K
	While K=0
		getKey→K
	End
	Output(S+4,1," 
	If K=25
		S-1→S
	If K=34
		S+1→S
	If S>2
		1→S
	If S<1
		2→S
End
If S=1
Then
	123698745→θ
	Goto 8
End
If S=2
Then
	ClrHome
	Return
End